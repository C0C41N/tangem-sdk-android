plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.android.extensions'
    id 'maven-publish'
}

apply from: '../artifactConfig.gradle'

android {
    compileSdkVersion 32
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 1
        versionName "$artifactConfig.version"
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = "$artifactConfig.group"
                artifactId = "$project.artifactId"
                version = "$artifactConfig.version"

                if (artifactId == "android") {
                    from components.release
                    artifact(sourceJar)

                    pom.withXml {
                        Node pomNode = asNode()
                        pomNode.dependencies.'*'.findAll() {
                            it.groupId.text() == artifactConfig.group
                        }.each() { it.parent().remove(it) }

                        pomNode.dependencies.'*'.findAll() {
                            it.scope.text() == 'runtime'
                        }.each { it.scope*.value = 'compile' }
                    }
                } else if (artifactId == "core") {
                    from components.java
                }
            }
        }
        repositories {
            maven {
                url = "https://nexus.tangem-tech.com/repository/maven-releases"
                credentials {
                    username = "$nexusUser"
                    password = "$nexusPass"
                }
            }
        }
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

dependencies {
    // internal
    implementation project(':tangem-sdk-core')

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$libs.versions.kotlin"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$libs.versions.kotlin"

    // coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.3.9"

    // android
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.1"
    implementation "androidx.biometric:biometric:1.1.0"

    // misc
    implementation 'com.skyfishjy.ripplebackground:library:1.0.1'
    implementation 'at.favre.lib:armadillo:1.0.0'

    // testing
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
